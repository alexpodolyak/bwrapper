plugins {
    id 'java'
    id 'checkstyle'
}

group 'com.bwrapper'
version '1.0.0'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

ext {
    junitVersion = '5.6.2'
    mockitoVersion = '3.3.3'
    checkstyleVersion = '8.32'
}

dependencies {
    testCompile group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: junitVersion
    testCompile group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: junitVersion
    testCompile group: 'org.junit.jupiter', name: 'junit-jupiter-params', version: junitVersion

    testCompile group: 'org.mockito', name: 'mockito-core', version: mockitoVersion
}

test {
    useJUnitPlatform()
}

jar {
    archiveName "${project.name}-${project.version}.jar"
    manifest {
        attributes 'Created-By': 'Alexander Podolyak'
        attributes 'Build-Jdk': JavaVersion.current()
        attributes 'Implementation-Title': project.name
        attributes 'Implementation-Version': project.version
    }
}

checkstyle {
    toolVersion checkstyleVersion
    ignoreFailures = false
    maxWarnings = 0
    configProperties = [
            'com.bwrapper.checkstyle.config': "${rootDir}\\config\\checkstyle"
    ]
}

checkstyleTest {
    enabled = false
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    archiveClassifier.set('javadoc')
    from javadoc.destinationDir
}

artifacts {
    archives javadocJar
}